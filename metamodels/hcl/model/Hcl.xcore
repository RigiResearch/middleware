@Ecore(nsPrefix="hcl", nsURI="http://www.rigiresearch.com/middleware/hcl/1.0.0")
@GenModel(
    bundleManifest = "false",
    modelDirectory = "model/build/xcore/main",
    complianceLevel = "8.0"
)
package com.rigiresearch.middleware.metamodels.hcl

class Specification {
	contains Resource[] resources
}

class Resource {
	String specifier
	String ^type
	String name
	derived id String fullyQualifiedName get {
        newArrayList(specifier, ^type, name)
            .filter[it !== null]
            .join(".")
	}
	contains Comment comment
	contains Value value
}

class Comment {
    String[] lines
}

// Type hierarchy

class Value {}

class NameValuePair {
	String name
	contains Value value
	contains Comment comment
}

class Dictionary extends Value {
	String name
	// elements may contain duplicate names
	contains NameValuePair[] elements
}

class List extends Value {
	contains Value[] elements
}

class Number extends Value {
	String value
}

class Text extends Value {
	String value
}

class Bool extends Value {
	boolean value
}

class TextExpression extends Value {
	// The text before and after the expression
	String before
	String after
	contains Reference reference
}

class Reference extends Value {}

class FunctionCall extends Reference {
	String function
	contains Value[] arguments
}

class ResourceReference extends Reference {
	String[] components
    derived String fullyQualifiedName get {
        newArrayList(components.toArray())
            .filter[it !== null]
            .join(".")
	}
}
