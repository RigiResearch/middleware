plugins {
  id "java-library"
  id "org.xtext.builder"
  id "org.xtext.xtend"
  id "maven-publish"
}

repositories {
  mavenLocal()
}

sourceSets {
  main {
    xtendOutputDir = "src/main/xtend-gen"
  }
  test {
    xtendOutputDir = "src/main/xtend-gen"
  }
}

dependencies {
  implementation project(":metamodels:hcl")
  implementation "org.eclipse.emf:compare:3.5.3.201909101346"
  implementation "org.eclipse.xtext:org.eclipse.xtext:${xtextVersion}"
  implementation "org.eclipse.xtext:org.eclipse.xtext.xbase:${xtextVersion}"
  implementation "org.eclipse.xtend:org.eclipse.xtend.lib:${xtextVersion}"
  testImplementation "org.eclipse.xtext:org.eclipse.xtext.testing:${xtextVersion}"
  testImplementation "org.eclipse.xtext:org.eclipse.xtext.xbase.testing:${xtextVersion}"
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId "org.eclipse.emf"
      artifactId "compare"
      version "3.5.3.201909101346"
      artifact source: file("./lib/org.eclipse.emf.compare_3.5.3.201909101346.jar"), classifier: null, extension: "jar"
    }
    maven(MavenPublication) {
      groupId "org.eclipse.emf"
      artifactId "compare"
      version "3.5.3.201909101346"
      artifact source: file("./lib/org.eclipse.emf.compare.source_3.5.3.201909101346.jar"), classifier: "sources", extension: "jar"
    }
  }
}
// TODO find out how to mix this with dependencies
// compileJava.dependsOn(publishMavenPublicationToMavenLocal)

configurations {
  mwe2 {
    extendsFrom compile
  }
}

dependencies {
  mwe2 "org.eclipse.emf:org.eclipse.emf.mwe2.launch:${mwe2Version}"
  mwe2 "org.eclipse.xtext:org.eclipse.xtext.xtext.generator:${xtextVersion}"
  mwe2 "org.eclipse.xtext:org.eclipse.xtext.common.types:${xtextVersion}"
  mwe2 "org.eclipse.xtext:xtext-antlr-generator:[2.1.1, 3)"
  // Xcore support
  mwe2("org.eclipse.emf:org.eclipse.emf.ecore.xcore:${xcoreVersion}") {
    exclude group: "org.antlr", module: "antlr-runtime"
  }
  mwe2 "org.eclipse.emf:org.eclipse.emf.codegen.ecore.xtext:${ecoreXtextVersion}"
}

// Publish to maven local
task generateXtextLanguage(type: JavaExec) {
  main = "org.eclipse.emf.mwe2.launch.runtime.Mwe2Launcher"
  classpath = configurations.mwe2
  inputs.file "src/main/java/com/rigiresearch/middleware/notations/hcl/GenerateHcl.mwe2"
  inputs.file "src/main/java/com/rigiresearch/middleware/notations/hcl/Hcl.xtext"
  outputs.dir "src/main/xtext-gen"
  args += "src/main/java/com/rigiresearch/middleware/notations/hcl/GenerateHcl.mwe2"
  args += "-p"
  args += "rootPath=/${projectDir}/.."
}

generateXtext.dependsOn(generateXtextLanguage)
clean.dependsOn(cleanGenerateXtextLanguage)
eclipse.classpath.plusConfigurations += [configurations.mwe2]

xtend {
  generator {
    //whether to generate @SuppressWarnings("all"), enabled by default
    suppressWarningsAnnotation = false
    //whether to generate the @Generated annotation, disabled by default
    generatedAnnotation {
      active = true
      includeDate = true
    }
  }
  debugger {
    sourceInstaller = 'SMAP'
    hideSyntheticVariables = true
  }
  validator {
    // Available levels are error, warning, info and ignore
    error 'org.eclipse.xtend.core.validation.IssueCodes.unused_private_member'
  }
}
